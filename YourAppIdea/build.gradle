buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "org.michenux.yourappidea"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 310
        versionName "3.1.0"

        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            if (project.hasProperty('yourappidea.keystore')) {
                storeFile file(project.property("yourappidea.keystore"))
                storePassword project.property("yourappidea.keystore.password")
                keyAlias project.property("yourappidea.keystore.keyAlias")
                keyPassword project.property("yourappidea.keystore.keyPassword")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty('yourappidea.keystore')) {
                signingConfig signingConfigs.release
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

dependencies {
    compile supportDependencies.appcompat
    compile supportDependencies.design
    compile supportDependencies.cardview
    compile supportDependencies.preferences
    compile otherDependencies.universalImageLoader
    compile otherDependencies.facebookSdk
    compile otherDependencies.retrofit
    compile otherDependencies.retrofitGsonConverter
    compile playServicesDependencies.maps
    compile playServicesDependencies.auth
    compile playServicesDependencies.ads
    compile playServicesDependencies.location

    compile rx.rxPermission
    compile rx.rxbinding
    compile rx.rxAndroid
    compile rx.retrofitRxJava

    compile project(':drodrolib')

    testCompile testDependencies.junit
    testCompile testDependencies.mockito
    testCompile testDependencies.hamcrest
}

apply plugin: 'com.google.gms.google-services'
